/*
 * SPDX-FileCopyrightText: 2015, microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    compile "com.squareup.wire:wire-runtime:$wireVersion"
}

sourceSets {
    main {
        java.srcDirs += 'src/main/protos-java'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = 'Wearable'
                    description = 'Library to work with Android Wear devices'
                    url = 'https://github.com/microg/Wearable'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'microg'
                            name = 'microG Team'
                        }
                        developer {
                            id = 'mar-v-in'
                            name = 'Marvin W.'
                        }
                    }
                    scm {
                        url = 'https://github.com/microg/Wearable'
                        connection = 'scm:git:https://github.com/microg/Wearable.git'
                        developerConnection = 'scm:git:ssh://github.com/microg/Wearable.git'
                    }
                }

                from components.java
            }
        }
        if (project.hasProperty('sonatype.username')) {
            repositories {
                maven {
                    name = 'sonatype'
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username project.getProperty('sonatype.username')
                        password project.getProperty('sonatype.password')
                    }
                }
            }
        }
    }
    if (project.hasProperty('signing.keyId')) {
        signing {
            sign publishing.publications
        }
    }
}
