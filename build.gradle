/*
 * SPDX-FileCopyrightText: 2015, microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.wireVersion = '1.6.1'
}

String gitDescribeVersion() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec { commandLine 'git', 'describe', '--tags', '--always', '--dirty'; standardOutput = stdout }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

allprojects {
    apply plugin: 'idea'

    group = 'org.microg'
    version = gitDescribeVersion()
}

subprojects {
    repositories {
        jcenter()
    }
}
